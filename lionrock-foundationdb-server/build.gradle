plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management'
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'signing'
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceCompatibility = '11'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    tests
    plain
}

bootJar {
    classifier = 'boot'
}

dependencies {
    implementation project(':lionrock-proto-java')
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.zipkin.brave:brave-instrumentation-grpc'
    implementation 'org.foundationdb:fdb-java:7.2.6'

    implementation "com.google.protobuf:protobuf-java"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'com.wavefront:wavefront-spring-boot-starter'

    implementation 'net.devh:grpc-spring-boot-starter:2.14.0.RELEASE'
    implementation "io.grpc:grpc-netty-shaded"

    testImplementation project(':lionrock-foundationdb-client')
    testImplementation(testFixtures(project(":lionrock-grpc-tests")))
    testImplementation "io.grpc:grpc-testing"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.platform:junit-platform-suite'
    testImplementation 'org.junit.platform:junit-platform-runner'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "com.google.guava:guava-bom:${guavaVersion}"
        mavenBom "io.grpc:grpc-bom:${grpcVersion}"
        mavenBom "com.google.protobuf:protobuf-bom:${protobufVersion}"
        mavenBom "com.wavefront:wavefront-spring-boot-bom:${wavefrontVersion}"
    }
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    publications {
        bootJava(MavenPublication) {
            from components.java
            artifact bootJar
            pom {
                name = "lionrock-foundationdb-server"
                description = "FoundationDB gRPC Server"
                url = "https://github.com/panghy/lionrock"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "panghy"
                        name = "Clement Pang"
                    }
                }
                scm {
                    url = "https://github.com/panghy/lionrock"
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.bootJava
}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveBaseName = "test-${project.archivesBaseName}"
    from sourceSets.test.output
}

artifacts {
    tests testJar
    plain jar
}