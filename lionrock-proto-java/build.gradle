plugins {
    id "com.google.protobuf" version "0.9.2"
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.spring.dependency-management'
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.protobuf:protobuf-java"
    implementation 'com.google.guava:guava'
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-stub"

    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

sourceSets {
    main {
        proto {
            srcDir "../proto"
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

apply plugin: 'idea'

clean {
    delete protobuf.generatedFilesBaseDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            from components.java
            pom {
                name = "lionrock-proto-java"
                description = "Lionrock Java Proto Library"
                url = "https://github.com/panghy/lionrock"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "panghy"
                        name = "Clement Pang"
                    }
                }
                scm {
                    url = "https://github.com/panghy/lionrock"
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "io.grpc:grpc-bom:${grpcVersion}"
        mavenBom "com.google.protobuf:protobuf-bom:${protobufVersion}"
    }
}